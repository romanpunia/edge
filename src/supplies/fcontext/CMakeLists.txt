set(FCTX_ARCHS arm arm64 loongarch64 mips32 mips64 ppc32 ppc64 riscv64 s390x i386 x86_64 combined)
math(EXPR FCTX_BITS "${CMAKE_SIZEOF_VOID_P}*8")
if (WIN32)
    set(FCTX_BIN pe)
elseif (APPLE)
    set(FCTX_BIN mach-o)
else()
    set(FCTX_BIN elf)
endif()
if (CMAKE_SYSTEM_PROCESSOR MATCHES "^[Aa][Rr][Mm]" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
    set(FCTX_ABI aapcs)
elseif (WIN32)
    set(FCTX_ABI ms)
elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^mips")
    if (FCTX_BITS EQUAL 32)
        set(FCTX_ABI o32)
    else()
        set(FCTX_ABI n64)
    endif()
else()
set(FCTX_ABI sysv)
endif()
if (CMAKE_SYSTEM_PROCESSOR IN_LIST FCTX_ARCHS)
    set(FCTX_ARCH ${CMAKE_SYSTEM_PROCESSOR})
elseif (FCTX_BITS EQUAL 32)
    if (CMAKE_SYSTEM_PROCESSOR MATCHES "^[Aa][Rr][Mm]")
        set(FCTX_ARCH arm)
    elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^mips")
        set(FCTX_ARCH mips32)
    else()
        set(FCTX_ARCH i386)
    endif()
else()
    if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64" OR CMAKE_SYSTEM_PROCESSOR MATCHES "^[Aa][Rr][Mm]")
        set(FCTX_ARCH arm64)
    elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^mips")
        set(FCTX_ARCH mips64)
    else()
        set(FCTX_ARCH x86_64)
    endif()
endif()
if (MSVC)
    if(FCTX_ARCH STREQUAL arm64 OR FCTX_ARCH STREQUAL arm)
        set(FCTX_ASM armasm)
    else()
        set(FCTX_ASM masm)
    endif()
else()
    set(FCTX_ASM gas)
endif()
if (FCTX_BIN STREQUAL pe)
    set(FCTX_EXT .asm)
elseif (FCTX_ASM STREQUAL gas)
    set(FCTX_EXT .S)
else()
    set(FCTX_EXT .asm)
endif()
if (FCTX_BIN STREQUAL mach-o)
    set(FCTX_BIN macho)
endif()
set(FCTX_SUFFIX ${FCTX_ARCH}_${FCTX_ABI}_${FCTX_BIN}_${FCTX_ASM})
if (EXISTS ${PROJECT_SOURCE_DIR}/src/supplies/fcontext/make_${FCTX_SUFFIX}${FCTX_EXT})
    set(ED_USE_FCTX ON CACHE BOOL "Enable fcontext implementation for coroutines (otherwise ucontext or WinAPI)")
    if (ED_USE_FCTX)
        set(FCTX_SOURCES
            ${PROJECT_SOURCE_DIR}/src/supplies/fcontext/fcontext.h
            ${PROJECT_SOURCE_DIR}/src/supplies/fcontext/make_${FCTX_SUFFIX}${FCTX_EXT}
            ${PROJECT_SOURCE_DIR}/src/supplies/fcontext/jump_${FCTX_SUFFIX}${FCTX_EXT}
            ${PROJECT_SOURCE_DIR}/src/supplies/fcontext/ontop_${FCTX_SUFFIX}${FCTX_EXT})
        list (APPEND SOURCE ${FCTX_SOURCES})
        message(STATUS "Async fcontext implementation: ${FCTX_SUFFIX}")
    else()
        message(STATUS "Async fcontext implementation: ucontext or WinAPI")
    endif()
else()
    set(ED_USE_FCTX OFF CACHE BOOL "FContext is unavailable")
    message("Async fcontext is unavailable on ${CMAKE_SYSTEM_NAME} ${CMAKE_SYSTEM_PROCESSOR}. Fallback to ucontext or WinAPI")
endif()
unset(FCTX_BIN)
unset(FCTX_ABI)
unset(FCTX_BITS)
unset(FCTX_ARCHS)
unset(FCTX_ARCH)
unset(FCTX_ASM)
unset(FCTX_EXT)
unset(FCTX_SUFFIX)