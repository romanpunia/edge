if (${CMAKE_SYSTEM_PROCESSOR} MATCHES "AMD64" 
    OR ${CMAKE_SYSTEM_PROCESSOR} MATCHES "x86_64"
    OR ${CMAKE_SYSTEM_PROCESSOR} MATCHES "i386"
    OR ${CMAKE_SYSTEM_PROCESSOR} MATCHES "x86")
    if (${CMAKE_SIZEOF_VOID_P} EQUAL 8)
        set(FCTX_ARCH x86_64)
    else()
        set(FCTX_ARCH i386)
    endif()
    set(FCTX_ACTIVE ON)
elseif(${CMAKE_SYSTEM_PROCESSOR} MATCHES "arm")
    if (CMAKE_CL_64)
        set(FCTX_ARCH arm64)
    else()
        set(FCTX_ARCH arm)
    endif()
    set(FCTX_ACTIVE ON)
else()
    set(FCTX_ACTIVE OFF)
endif()
if (FCTX_ACTIVE)
    set(TH_WITH_FCTX ON CACHE BOOL "Enable FContext built-in library")
    if (TH_WITH_FCTX)
        if (WIN32)
            set(FCTX_PLATFORM ms)
            set(FCTX_COFF pe)
            set(FCTX_ASM_SOURCE_EXTENSION .asm)
            if (MINGW)
                set(FCTX_ASM_COMPILER gas)
            else()
                set(FCTX_ASM_COMPILER masm)
            endif()
        else()
            set(FCTX_PLATFORM sysv)
            set(FCTX_COFF elf)
            set(FCTX_ASM_SOURCE_EXTENSION .S)
            set(FCTX_ASM_COMPILER gas)
        endif()    
        set(FCTX_SOURCE_SUFFIX ${FCTX_ARCH}_${FCTX_PLATFORM}_${FCTX_COFF}_${FCTX_ASM_COMPILER}${FCTX_ASM_SOURCE_EXTENSION})
        set(FCTX_SOURCE_ASSEMBLY
            ${CMAKE_CURRENT_LIST_DIR}/asm/make_${FCTX_SOURCE_SUFFIX}
            ${CMAKE_CURRENT_LIST_DIR}/asm/jump_${FCTX_SOURCE_SUFFIX}
            ${CMAKE_CURRENT_LIST_DIR}/asm/ontop_${FCTX_SOURCE_SUFFIX})
        list (APPEND SOURCE
            ${CMAKE_CURRENT_LIST_DIR}/fcontext.h
            ${FCTX_SOURCE_ASSEMBLY})
        message(STATUS "FContext platform: ${FCTX_ARCH}_${FCTX_PLATFORM}_${FCTX_COFF}_${FCTX_ASM_COMPILER}")
    endif()
else()
    set(TH_WITH_FCTX OFF CACHE BOOL "FContext is unavailable")
    message("FContext is unavailable on ${CMAKE_SYSTEM_PROCESSOR}")
endif()
unset(FCTX_ACTIVE)
unset(FCTX_ARCH)
unset(FCTX_PLATFORM)
unset(FCTX_COFF)
unset(FCTX_ASM_COMPILER)
unset(FCTX_ASM_SOURCE_EXTENSION)
unset(FCTX_SOURCE_SUFFIX)